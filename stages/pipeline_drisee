#!/usr/bin/env perl 

use strict;
use warnings;
no warnings('once');

use Pipeline;
use Pipeline_Conf;

use Getopt::Long;
use File::Copy;
umask 000;

my $stage_name="drisee";
my $stage;
for my $s (@{$Pipeline_Conf::pipeline->{'default'}}) {
  $stage = $s if $s->{name} eq $stage_name; 
}
my $stage_id = $stage->{id} || '075';
my $revision = "0";
my $version  = $Pipeline_Conf::pipeline_version.".".$revision;

# options
my $job_num = "";
my $seqs    = "";
my $name    = "raw";
my $derep   = "";
my $procs   = 2;
my $filter  = 0;
my $ver     = 0;
my $help    = 0;
my $options = GetOptions ("job=i"    => \$job_num,
			  "seqs=s"   => \$seqs,
			  "name:s"   => \$name,
			  "derep:s"  => \$derep,
			  "procs:i"  => \$procs,
			  "filter!"  => \$filter,
			  "version!" => \$ver,
			  "help!"    => \$help,
			 );
if ( $ver ) {
  print STDERR "$stage_name - $version - $stage_id\n";
  exit(0);
} elsif ( $help or !($job_num and $seqs) ) {
  print STDERR "Usage: pipeline_$stage_name -j <job number> -s <sequence file> [-n <name of input seqs stage, default '$name'> -d <derep file> -f <filter seqs, bool> -p <# processors, default '$procs'>]\n";
  exit(1);
}

my $log = Pipeline::logger($job_num);
$log->info("Starting $stage_name on job: $job_num");

unless (-s $seqs) {
  $log->error("file: $seqs does not exist");
  exit(1);
}

# update jobcache stage status
Pipeline::update_stage_info($job_num, $stage_name, "running");

my $job_dir     = $Pipeline_Conf::global_job_dir."/".$job_num;
my $proc_dir    = $job_dir."/proc";
my $stage_dir   = $proc_dir."/".$stage_id.".".$stage_name;
my $results_dir = $job_dir."/".$Pipeline_Conf::results_dir;
my $run_dir     = $stage_dir;
my $hostname    = `hostname`;
chomp $hostname;

# create directories
if (-d $stage_dir) {
  my $t = time;
  $log->info("found $stage_dir, moving to $stage_dir.$t");
  move($stage_dir, $stage_dir.".".$t) or fail($log, "$!");
}
mkdir($stage_dir) or fail($log, "Could not mkdir: $stage_dir, $!");

# set cluster workspace dir (use local if large job)
my $work_dir = (($procs >= 4) && (-d $Pipeline_Conf::local_workspace)) ? $Pipeline_Conf::local_workspace : $Pipeline_Conf::cluster_workspace;
if (-d $work_dir) {
  $run_dir = "$work_dir/$job_num.$stage_id.$stage_name";
  if (-d $run_dir) {
    system("rm -rf $run_dir");
  }
  mkdir($run_dir) or fail($log, "Could not mkdir: $run_dir, $!");
  system("echo $hostname > $stage_dir/hostname");
}
$log->info("Running on host $hostname, using dir $run_dir");

my $tmp_dir  = ((-d $Pipeline_Conf::local_tmp) && ($procs >= 4)) ? $Pipeline_Conf::local_tmp : ((-d $Pipeline_Conf::cluster_tmp) ? $Pipeline_Conf::cluster_tmp : $run_dir);
my $prefix   = $stage_id.".".$stage_name;
my $err_file = $run_dir."/".$prefix.".out";
my $log_file = $run_dir."/".$prefix.".log";
my $message  = "$stage_name failed on job: $job_num, see $err_file for details.";

# run
my $format  = '';
my $gzipped = 0;
if ($seqs =~ /^(\S+)\.gz$/) {
  $gzipped = 1;
  $format = ($1 =~ /\.(fq|fastq)$/) ? 'fastq' : 'fasta';
}
else {
  $format = ($seqs =~ /\.(fq|fastq)$/) ? 'fastq' : 'fasta';
}
my $infile  = $run_dir."/".$prefix.".".$format;
my $outfile = $run_dir."/".$prefix.".stats";

if ($gzipped) {
  system("zcat $seqs > $infile") == 0 or fail($log, "gunzip $seqs: ".$message);
} else {
  system("cp $seqs $infile") == 0 or fail($log, "cp $seqs: ".$message);
}
system("drisee -v -p $procs -t $format -d $tmp_dir -l $log_file".($filter ? ' -f' : '').($derep ? " -r $derep" : '')." $infile $outfile >> $err_file 2>&1") == 0 or fail($log, "drisee: ".$message);

# load stats
my $drisee = 0;
if (-s $outfile) {
  $drisee = `head -2 $outfile | tail -1 | cut -f8`;
  chomp $drisee;
}
my $res = Pipeline::set_job_statistics($job_num, [["drisee_score_$name", sprintf("%.3f", $drisee)]]);
unless ($res) {
  fail($log, "$stage_name failed on job: $job_num, loading drisee_score_$name stat: $drisee");
}  

# cleanup
move($outfile, "$results_dir/$prefix.stats") or fail($log, "Failed copy: $prefix.stats - $!");
move($err_file, "$results_dir/$prefix.info") or fail($log, "Failed copy: $prefix.info - $!");
chmod 0666, "$results_dir/$prefix.stats";
chmod 0666, "$results_dir/$prefix.info";

# move run_dir to stage_dir if on local
if ($run_dir ne $stage_dir) {
  system("mv $run_dir/*.out $stage_dir/.");
  system("mv $run_dir/*.log $stage_dir/.");
  system("mv $run_dir/*.err $stage_dir/.");
  system("rm -rf $run_dir") == 0 or fail($log, "$stage_name failed on job: $job_num, can not remove $run_dir");
} else {
  my @run_files = `ls $run_dir`;
  chomp @run_files;
  map { system("rm $run_dir/$_") } grep { $_ !~ /\.(out|err|log)$/ } @run_files;
}

$log->info("Finished $stage_name on job: $job_num");

# update jobcache stage status
Pipeline::update_stage_info($job_num, $stage_name, "completed");

exit(0);

sub fail {
  my ($log, $message) = @_;
  Pipeline::update_stage_info($job_num, $stage_name, "error");
  $log->error($message);
  if ($run_dir ne $stage_dir) {
    system("mv $run_dir/* $stage_dir/.");
    system("rmdir $run_dir");
  }
  exit(1);
}
